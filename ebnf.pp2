/**
* DBML (database markup language) is a simple, readable DSL language designed to define database structures.
* @see https://www.dbml.org
*/
%skip   T_WHITESPACE                \s+
%skip   T_COMMENT                   //[^\n]*\n

/**
* --------------------------------------------------------------------------
*  Values
* --------------------------------------------------------------------------
*/
%token T_BOOL_TRUE                  (?<=\b)true\b
%token T_BOOL_FALSE                 (?<=\b)false\b
%token T_NULL                       (?<=\b)null\b


%token  T_PROJECT                   (?<=\b)Project|project\b
%token  T_TABLE                     (?<=\b)Table|table\b
%token  T_TABLE_ALIAS               (?<=\b)as\b
%token  T_TABLE_INDEXES             (?<=\b)(Indexes|indexes)\b
%token  T_TABLE_REF                 (Ref:|ref:)
%token  T_TABLE_GROUP               (?<=\b)TableGroup\b
%token  T_ENUM                      (?<=\b)(Enum|enum)\b
%token  T_TABLE_SETTING_PK          (?<=\b)(primary\skey|pk)\b
%token  T_TABLE_SETTING_UNIQUE      (?<=\b)unique\b
%token  T_TABLE_SETTING_INCREMENT   (?<=\b)increment\b
%token  T_TABLE_SETTING_DEFAULT     (?<=\b)default\b

%token  T_TABLE_SETTING_NOT_NULL    (?<=\b)not\snull\b

%token  T_REF_ACTION_CASCADE        (?<=\b)cascade\b
%token  T_REF_ACTION_RESTRICT       (?<=\b)restrict\b
%token  T_REF_ACTION_SET_NULL       (?<=\b)set\snull\b
%token  T_REF_ACTION_SET_DEFAULT    (?<=\b)set\default\b
%token  T_REF_ACTION_NO_ACTION      (?<=\b)no\saction\b

%token  T_REF_ACTION_DELETE         (?<=\b)delete\b
%token  T_REF_ACTION_UPDATE         (?<=\b)update\b

%token  T_SETTING_NOTE          note:
%token  T_NOTE                  (?<=\b)Note\b

/**
 * --------------------------------------------------------------------------
 *  Numbers
 * --------------------------------------------------------------------------
 */
%token  T_FLOAT                 [0-9]+\.[0-9]+                      // 123.123
%token  T_INT                   [0-9]+                              // 123

/**
 * --------------------------------------------------------------------------
 *  String
 * --------------------------------------------------------------------------
 */
%token  T_QUOTED_STRING         ('{3}|["']{1})([^'"][\s\S]*?)\1     // 'string' "string" '''string'''
%token  T_EXPRESSION            (`{1})([\s\S]+?)\1                  // `string`
%token  T_WORD                  [a-zA-Z0-9_]+                       // table_name1

/**
* --------------------------------------------------------------------------
*  Syntax
* --------------------------------------------------------------------------
*/
%token  T_EOL               \\n
%token  T_LPAREN            \(
%token  T_RPAREN            \)
%token  T_LBRACE            {
%token  T_RBRACE            }
%token  T_LBRACK            \[
%token  T_RBRACK            \]
%token  T_GT                \>
%token  T_LT                \<
%token  T_COMMA             ,
%token  T_COLON             :
%token  T_MINUS             \-
%token  T_DOT               \.


/**
* --------------------------------------------------------------------------
*  DBML Grammar
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs
*/
#Document
:
    Schema()
;

#Schema -> {
   return new \Butschster\Dbml\Ast\SchemaNode($children);
}
:
    (
        Project() |
        Table() |
        TableGroup() |
        Enum() |
        (Ref() EOL())
    )*
;

/**
* --------------------------------------------------------------------------
*  Project
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#project-definition
*/
#Project -> {
    return new \Butschster\Dbml\Ast\ProjectNode(
        $token->getOffset(), $children
    );
}
:
    ::T_PROJECT:: ProjectName() ::T_LBRACE:: EOL()
    (
        (Note() EOL()) |
        ProjectSetting()
    )*
    ::T_RBRACE:: EOL()
;

#ProjectName -> {
    return new \Butschster\Dbml\Ast\Project\NameNode(
        $token->getOffset(), \end($children)
    );
}
:
    String()
;

#ProjectSetting -> {
    return new \Butschster\Dbml\Ast\Project\SettingNode(
        $token->getOffset(), \current($children), \end($children)
    );
}
:
    ProjectSettingKey() ::T_COLON:: String() EOL()
;

#ProjectSettingKey -> {
    return new \Butschster\Dbml\Ast\Project\SettingKeyNode(
        $token->getOffset(), $children->getValue()
    );
}
:
    <T_WORD>
;

/**
* --------------------------------------------------------------------------
*  Table
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#table-definition
*/
#Table -> {
    return new \Butschster\Dbml\Ast\TableNode(
        $token->getOffset(), $children[0]->getValue(), $children
    );
}
:
    ::T_TABLE:: TableName() TableAlias()? ::T_LBRACE:: EOL()
    (TableColumn() EOL())*
    (Note() | SettingNote() EOL())?

    TableIndexes()?
    ::T_RBRACE:: EOL()
;

#TableName -> {
    return new \Butschster\Dbml\Ast\Table\NameNode(
        $token->getOffset(), $children->getValue()
    );
}
:
    <T_WORD> | <T_INT>
;

#TableAlias -> {
   return new \Butschster\Dbml\Ast\Table\AliasNode(
       $token->getOffset(), $children[0]->getValue()
   );
}
:
    ::T_TABLE_ALIAS:: <T_WORD>
;

/**
* --------------------------------------------------------------------------
*  Table Column
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#column-definition
*/

#TableColumn -> {
    return new \Butschster\Dbml\Ast\Table\ColumnNode(
        $token->getOffset(), $children[0], $children[1], array_slice($children, 2)
    );
}
:
    TableColumnName() TableColumnType() TableColumnSettings()?
;

#TableColumnName -> {
    return new \Butschster\Dbml\Ast\Table\Column\NameNode(
        $token->getOffset(), $children->getValue()
    );
}
:
    <T_WORD>
;

#TableColumnType -> {
    return new \Butschster\Dbml\Ast\Table\Column\TypeNode(
        $token->getOffset(), $children[0], $children[1] ?? null
    );
}
:
   TableColumnTypeName() TableColumnTypeSize()?
;

#TableColumnTypeName -> {
    return new \Butschster\Dbml\Ast\Table\NameNode(
        $token->getOffset(), $children->getValue()
    );
}
:
    <T_WORD>
;

#TableColumnTypeSize
:
    ::T_LPAREN:: Int() ::T_RPAREN::
;

TableColumnSettings
:
    ::T_LBRACK::
    (
        SettingNote() |
        TableColumnRef() |
        TableColumnSettingWithValue() |
        TableColumnSetting()
    )*
    ::T_RBRACK::
;

TableColumnSetting
:
    ColumnSettingKeyword() ::T_COMMA::?
;

ColumnSettingKeyword
:
    ColumnSettingPrimaryKey() |
    ColumnSettingUnique() |
    ColumnSettingIncrement() |
    ColumnSettingNull() |
    ColumnSettingNotNull()
;

ColumnSettingKeyValue
:
    <T_TABLE_SETTING_DEFAULT>
;

ColumnSettingNull -> {
    return new \Butschster\Dbml\Ast\Table\Column\Setting\NullNode(
      $token->getOffset()
  );
}
:
    <T_NULL>
;

ColumnSettingNotNull -> {
    return new \Butschster\Dbml\Ast\Table\Column\Setting\NotNullNode(
      $token->getOffset()
  );
}
:
    <T_TABLE_SETTING_NOT_NULL>
;

ColumnSettingIncrement -> {
    return new \Butschster\Dbml\Ast\Table\Column\Setting\IncrementNode(
      $token->getOffset()
  );
}
:
    <T_TABLE_SETTING_INCREMENT>
;

ColumnSettingPrimaryKey -> {
    return new \Butschster\Dbml\Ast\Table\Column\Setting\PrimaryKeyNode(
      $token->getOffset()
  );
}
:
    <T_TABLE_SETTING_PK>
;

ColumnSettingUnique -> {
    return new \Butschster\Dbml\Ast\Table\Column\Setting\UniqueNode(
      $token->getOffset()
  );
}
:
    <T_TABLE_SETTING_UNIQUE>
;

TableColumnSettingWithValue -> {
    return new \Butschster\Dbml\Ast\Table\Column\SettingWithValueNode(
        $token->getOffset(), $children[0]->getValue(), $children[1]
    );
}
:
    ColumnSettingKeyValue() ::T_COLON::
    (
        Expression() |
        Float() |
        Int() |
        Boolean() |
        Null() |
        String()
    ) ::T_COMMA::?
;

/**
* --------------------------------------------------------------------------
*  Table Index
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#index-definition
*/
TableIndexes
:
    ::T_TABLE_INDEXES::  ::T_LBRACE:: EOL()
    (TableIndex() EOL())*
    ::T_RBRACE::
;

#TableIndex -> {
    return new \Butschster\Dbml\Ast\Table\IndexNode(
        $token->getOffset(), $children[0], array_slice($children, 1)
    );
}
:
    TableIndexFiled() TableIndexSettings()?
;

TableIndexFiled
:
    TableIndexCompositeField() |
    TableIndexSingleField()
;

#TableIndexSingleField -> {
    return new \Butschster\Dbml\Ast\Table\Index\FieldsNode(
      $token->getOffset(), [$children]
    );
}
:
    String()|Expression()
;

#TableIndexCompositeField -> {
    return new \Butschster\Dbml\Ast\Table\Index\FieldsNode(
       $token->getOffset(), $children
    );
 }
:
    ::T_LPAREN:: ((String()|Expression()) ::T_COMMA::?)* ::T_RPAREN::
;

#TableIndexSettings
:
    ::T_LBRACK::
    (
        SettingNote() |
        TableIndexSettingWithValue() |
        TableIndexSetting()
    )*
    ::T_RBRACK::
;

#TableIndexSetting
:
    IndexSettingName() ::T_COMMA::?
;

IndexSettingName
:
     ColumnSettingPrimaryKey() |
     ColumnSettingUnique()
;

#TableIndexSettingWithValue -> {
    return new \Butschster\Dbml\Ast\Table\Column\SettingWithValueNode(
        $token->getOffset(), $children[0]->getValue(), $children[1]
    );
}
:
    <T_WORD> ::T_COLON:: String() ::T_COMMA::?
;

/**
* --------------------------------------------------------------------------
*  Relationships & Foreign Key
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#relationships-foreign-key-definitions
*/
#Ref
:
    ::T_TABLE_REF::
    (RefShort() | RefLong())
;

#TableColumnRef -> {
    return new \Butschster\Dbml\Ast\RefNode(
        $token->getOffset(), $children
    );
}
:
    ::T_TABLE_REF:: TableColumnRefColumns() ::T_COMMA::?
;



TableColumnRefColumns -> {
    return new \Butschster\Dbml\Ast\Ref\ColumnsNode(
        $token->getOffset(), $children
    );
}
:
    RefType() RefRightTable()
;

#RefName -> {
    return new \Butschster\Dbml\Ast\Ref\NameNode(
        $token->getOffset(), $children->getValue()
    );
}
:
    <T_WORD>
;

RefShort -> {
    return new \Butschster\Dbml\Ast\RefNode(
        $token->getOffset(), $children
    );
}
:
    RefName()? ::T_COLON:: RefColumns()
;

RefLong -> {
   return new \Butschster\Dbml\Ast\Ref\GroupNode(
       $token->getOffset(), $children
   );
}
:
    RefName()?
    ::T_LBRACE:: EOL()
    RefColumns()*
    ::T_RBRACE:: EOL()
;

RefColumns -> {
    return new \Butschster\Dbml\Ast\Ref\ColumnsNode(
        $token->getOffset(), $children
    );
}
:
    RefLeftTable() RefType() RefRightTable() RefActions()?
;

RefType
:
    RefTypeManyToOne() | RefTypeOneToMany() | RefTypeOneToOne()
;

RefActions
:
    ::T_LBRACK::
    (
        OnUpdateAction() |
        OnDeleteAction()
    )*
    ::T_RBRACK::
;

OnUpdateAction -> {
   return new \Butschster\Dbml\Ast\Ref\Action\OnUpdateNode(
       $token->getOffset(), $children[1]->getValue()
   );
}
:
    <T_REF_ACTION_UPDATE>
    ::T_COLON::
    RefAction()
;

OnDeleteAction -> {
   return new \Butschster\Dbml\Ast\Ref\Action\OnDeleteNode(
       $token->getOffset(), $children[1]->getValue()
   );
}
:
    <T_REF_ACTION_DELETE>
    ::T_COLON::
    RefAction()
;

RefAction
:
    (
        <T_REF_ACTION_CASCADE> |
        <T_REF_ACTION_RESTRICT> |
        <T_REF_ACTION_SET_NULL> |
        <T_REF_ACTION_SET_DEFAULT> |
        <T_REF_ACTION_NO_ACTION>
    )
    ::T_COMMA::?
;


RefTypeManyToOne -> {
    return new \Butschster\Dbml\Ast\Ref\Type\ManyToOneNode(
       $token->getOffset()
    );
}
:
    <T_GT>
;

RefTypeOneToOne -> {
    return new \Butschster\Dbml\Ast\Ref\Type\OneToOneNode(
       $token->getOffset()
   );
}
:
    <T_MINUS>
;

RefTypeOneToMany -> {
    return new \Butschster\Dbml\Ast\Ref\Type\OneToManyNode(
       $token->getOffset()
   );
}
:
    <T_LT>
;

#RefLeftTable -> {
    return new \Butschster\Dbml\Ast\Ref\LeftTableNode(
        $token->getOffset(), $children[0], array_slice($children, 1)
    );
}
:
    TableName() ::T_DOT:: (TableColumnName() | RefCompositeTableColumn())
;

#RefRightTable -> {
   return new \Butschster\Dbml\Ast\Ref\RightTableNode(
       $token->getOffset(), $children[0], array_slice($children, 1)
   );
}
:
    TableName() ::T_DOT:: (TableColumnName() | RefCompositeTableColumn())
;

RefCompositeTableColumn
:
    ::T_LPAREN:: (TableColumnName() ::T_COMMA::?)* ::T_RPAREN::
;

/**
* --------------------------------------------------------------------------
*  Table group
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#tablegroup
*/
#TableGroup -> {
    return new \Butschster\Dbml\Ast\TableGroupNode(
        $token->getOffset(), $children[0]->getValue(), array_slice($children, 1)
    );
}
:
    ::T_TABLE_GROUP:: TableGroupName() ::T_LBRACE:: EOL()
    (TableName() EOL())*
    ::T_RBRACE:: EOL()
;

#TableGroupName
:
    <T_WORD>
;

/**
* --------------------------------------------------------------------------
*  Enum
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#enum-definition
*/
#Enum -> {
    return new \Butschster\Dbml\Ast\EnumNode(
        $token->getOffset(), $children[0]->getValue(), array_slice($children, 1)
    );
}
:
    ::T_ENUM:: EnumName() ::T_LBRACE:: EOL()
    (EnumValue()  EOL())*
    ::T_RBRACE:: EOL()
;

#EnumName
:
    <T_WORD>
;

#EnumValue -> {
     if ($token->getName() == "T_WORD") {
                    return new \Butschster\Dbml\Ast\Enum\ValueNode(
                        $token->getOffset(), $children[0]->getValue(), $children[1] ?? null
                    );
                }

                //is T_QUOTED_STRING
                return new \Butschster\Dbml\Ast\Enum\ValueNode(
                    $token->getOffset(), $children[0][1]->getValue(), $children[1] ?? null
                );
}
:
    <T_WORD> EnumNote()?|<T_QUOTED_STRING> EnumNote()?
;

EnumNote
:
     ::T_LBRACK::
    SettingNote()
    ::T_RBRACK::
;

/**
* --------------------------------------------------------------------------
*  Notes
* --------------------------------------------------------------------------
* @see https://www.dbml.org/docs/#note-definition
*/
#Note -> {
    return new \Butschster\Dbml\Ast\NoteNode(
        $token->getOffset(), \end($children)
    );
}
:
    ::T_NOTE::
    (
        (
            ::T_COLON:: String()
        ) |
        (
            ::T_LBRACE:: EOL()
            String()
            ::T_RBRACE:: EOL()
        )
    )
;

#SettingNote -> {
    return new \Butschster\Dbml\Ast\NoteNode(
        $token->getOffset(), \end($children)
    );
}
:
    ::T_SETTING_NOTE:: String() ::T_COMMA::?
;

/**
* --------------------------------------------------------------------------
*  Helpers
* --------------------------------------------------------------------------
*
*/
#Expression -> {
   return new \Butschster\Dbml\Ast\Values\ExpressionNode(
       $token->getOffset(), $children->getValue()
   );
}
:
    <T_EXPRESSION>
;

#Boolean -> {
    return new \Butschster\Dbml\Ast\Values\BooleanNode(
        $token->getOffset(),
        $children->getName() === 'T_BOOL_TRUE'
    );
}
:
    <T_BOOL_TRUE> | <T_BOOL_FALSE>
;

#Null -> {
    return new \Butschster\Dbml\Ast\Values\NullNode($token->getOffset());
}
:
    <T_NULL>
;

#Float -> {
    return new \Butschster\Dbml\Ast\Values\FloatNode(
        $token->getOffset(), $children->getValue()
    );
}
:
    <T_FLOAT>
;

#Int -> {
   return new \Butschster\Dbml\Ast\Values\IntNode(
       $token->getOffset(), $children->getValue()
   );
}
:
    <T_INT>
;

#String -> {
    return new \Butschster\Dbml\Ast\Values\StringNode(
        $token->getOffset(),
        $children
    );
}
:
    <T_WORD> | <T_QUOTED_STRING>
;

EOL
:
    ::T_EOL::*
;
